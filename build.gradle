buildscript {
  ext.kotlinVersion = '1.3.72'
  ext.junitVersion = '4.13'
  ext.assertjVersion = '3.15.0'
  ext.agpVersion = '3.6.0'
  ext.dokkaVersion = '0.9.18'
  ext.retrofitVersion = '2.7.1'
  ext.moshiVersion = '1.8.0'

  repositories {
    mavenCentral()
    google()
    gradlePluginPortal()
    maven{
      url="http://172.16.6.241:8081/repository/maven-snapshots/"
    }
  }

  dependencies {
    classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlinVersion"
    classpath 'com.gradle.publish:plugin-publish-plugin:0.11.0'
    classpath 'com.github.ben-manes:gradle-versions-plugin:0.27.0'
    classpath 'com.vv.life.plugin:gradle-publish-plugin:1.0-SNAPSHOT'
  }
}

apply plugin: 'java-library'
apply plugin: 'java-gradle-plugin'
apply plugin: 'kotlin'
apply plugin: 'kotlin-kapt'
apply plugin: 'com.github.ben-manes.versions'
apply plugin: "com.vv.life.plugin"
apply plugin: 'com.gradle.plugin-publish'
apply from: "gradle/integration-test.gradle"

gradlePlugin {
  plugins {
    mavenPublishPlugin {
      id = 'com.vv.life.plugin'
      implementationClass = 'com.vv.life.publish.MavenPublishPlugin'
    }
  }
}

repositories {
  mavenCentral()
  google()
  jcenter()
  maven{
    url="http://172.16.6.241:8081/repository/maven-snapshots/"
  }
}

dependencies {
  api gradleApi()
  api "org.jetbrains.kotlin:kotlin-stdlib:$kotlinVersion"

  implementation "org.jetbrains.dokka:dokka-gradle-plugin:$dokkaVersion"
  implementation "org.jetbrains.dokka:dokka-android-gradle-plugin:$dokkaVersion"

  compileOnly "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlinVersion"
  compileOnly "com.android.tools.build:gradle:$agpVersion"

  kapt "com.squareup.moshi:moshi-kotlin-codegen:$moshiVersion"

  implementation "com.squareup.moshi:moshi:$moshiVersion"
  implementation "com.squareup.retrofit2:retrofit:$retrofitVersion"
  implementation "com.squareup.retrofit2:converter-moshi:$retrofitVersion"

  testImplementation "junit:junit:$junitVersion"
  testImplementation "org.assertj:assertj-core:$assertjVersion"
  testImplementation "com.android.tools.build:gradle:$agpVersion"
  testImplementation 'com.github.stefanbirkner:system-rules:1.19.0'
  testImplementation "org.jetbrains.kotlin:kotlin-reflect:$kotlinVersion"
  testImplementation "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlinVersion"

  integrationTestImplementation gradleTestKit()
  integrationTestImplementation "junit:junit:$junitVersion"
  integrationTestImplementation "org.assertj:assertj-core:$assertjVersion"
}

sourceCompatibility = JavaVersion.VERSION_1_7

pluginBundle {
  website = POM_URL
  vcsUrl = POM_SCM_URL

  plugins {
    mavenPublishPlugin {
      displayName = POM_NAME
      tags = ['gradle', 'android', 'kotlin', 'maven', 'publish', 'library']
      description = POM_DESCRIPTION
    }
  }
}

wrapper {
  gradleVersion = '5.4.1'
  distributionType = Wrapper.DistributionType.ALL
}

configurations.all {
  resolutionStrategy {
    force "org.jetbrains.kotlin:kotlin-reflect:$kotlinVersion"
    force "org.jetbrains.kotlin:kotlin-stdlib:$kotlinVersion"
  }
}

test {
  testLogging {
    events "passed", "skipped", "failed"
    exceptionFormat "full"
  }
}
